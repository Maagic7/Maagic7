FUNCTION m7_RoundReal : REAL
    TITLE = 'Round Real to a fixed No. of digits (fixed point)'

// ==============================================================================
// ROUND a REAL TO a fixed No. OF digits (fixed point)
// Example: NoOfDigits := 3, rIN:=1.123456; RET_VAL=1.123
// Possible range OF digits is [0..5], because REAL has MAX. 6 digits after '.'
// ==============================================================================
//   
// AUTHOR: Stefan Maag, Dipl.-Ing. (FH) Elektrotechnik 
// DATE: 3/2017
//
// CHANGELOG:
// ------------------------------------------------------------------------------
// DATE         NAME        DESCRIPTION
// ------------------------------------------------------------------------------
// 27.07.2017   S.Maag      Covertet the old AWL to SCL
// ------------------------------------------------------------------------------

    VERSION : '1.0'
    AUTHOR  : 'S.Maag'
    FAMILY  : 'Maagic7'


VAR_INPUT
    rIN : REAL;             // The REAL value to round
    iNoOfDigits : INT;      // No. of digits after '.'
END_VAR

VAR_TEMP
    rFakt : REAL;
END_VAR

BEGIN
    IF iNoOfDigits >=0 AND iNoOfDigits <=5 THEN
        // 1st create the factor 1/10/100/1.000/10.000 as BCD, by shifting a '1' left NoOfDigits
        // each digit in a BCD value has 4 Bits, so we have to shift NoOfDigits*4
        rFakt := DINT_TO_REAL(BCD_TO_DINT (SHL(IN:=DW#16#1, N:=iNoOfDigits*4)));
        // Round the REAL by creating a fixed point DINT and then back to floating point REAL
        m7_RoundReal := DINT_TO_REAL(REAL_TO_DINT (rIN * rFakt)) / rFakt ;
    ELSE
        m7_RoundReal := rIN;
    END_IF;
    OK := TRUE; // ENO
END_FUNCTION

